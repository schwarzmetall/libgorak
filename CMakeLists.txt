cmake_minimum_required(VERSION 3.18)

include(CMakeDependentOption)

set(CMAKE_C_STANDARD 23)
set(CMAKE_C_EXTENSIONS FALSE)
set(CMAKE_C_STANDARD_REQUIRED TRUE)
set(CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)

project(libgorak LANGUAGES C)

# lgk_tnt configuration (defaults shall match lgk_tnt.h)
option(LGK_TNT_NO_ERRNO "don't add errno-aware macros (removes errno.h and string.h dependencies)" OFF)
option(LGK_TNT_NO_LEVEL "don't support trace message levels" OFF)
option(LGK_TNT_NO_LEVEL_SHORTS "don't add shorthand macros (DBG,INF,WARN,...)" OFF)
option(LGK_TNT_FULL_PATH "use full path in trace output" OFF)
option(LGK_TNT_OUTPUT_LEVEL_DYNAMIC "make trace level runtime-configurable" OFF)
set(LGK_TNT_OUTPUT_LEVEL_STATIC "DEBUG" CACHE STRING "static trace output level (TRAP, ERROR, WARNING, INFO, DEBUG)")
set(LGK_TNT_PRINT_DEFAULT_OVERRIDE "" CACHE STRING "override default function for error output")
set(LGK_TNT_TRAP_LABEL_PREFIX_OVERRIDE "" CACHE STRING "prefix for trap labels")

if (CMAKE_C_COMPILER_ID STREQUAL "GNU")
    add_compile_options(-Wall -Wextra -pedantic)
    add_compile_options(-O3 -ffast-math)
    add_compile_options(-march=native)
else()
    message(WARNING "please add optimization flags for your compiler (\"${CMAKE_C_COMPILER_ID}\")")
endif()

set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_util.h")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_char.h" "char.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_array.h" "array_find_int.c" "array_fill_int.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_date.h" "date_misc.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_timespec.h" "timespec_ms.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_fd.h" "readloop.c" "writeloop.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_tnt.h" "tnt.c" "trace_print_default.c" "trace_print_default_nolevel.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_queue.h")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_queue_ptr.h" "queue_ptr.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_queue_int.h" "queue_int.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_queue_uint.h" "queue_uint.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_threadpool.h" "threadpool.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_threads.h" "mtx_timedlock_ms.c")
set(LIBGORAK_SOURCES ${LIBGORAK_SOURCES} "lgk_fdset_input.h" "fdset_input.c")

add_library(libgorak ${LIBGORAK_SOURCES})
target_include_directories(libgorak PUBLIC ${CMAKE_CURRENT_SOURCE_DIR})

if (LGK_TNT_NO_ERRNO)
    target_compile_definitions(libgorak PUBLIC LGK_TNT_NO_ERRNO)
endif()
if (LGK_TNT_NO_LEVEL)
    target_compile_definitions(libgorak PUBLIC LGK_TNT_NO_LEVEL)
endif()
if (LGK_TNT_NO_LEVEL_SHORTS)
    target_compile_definitions(libgorak PUBLIC LGK_TNT_NO_LEVEL_SHORTS)
endif()
if (LGK_TNT_FULL_PATH)
    target_compile_definitions(libgorak PUBLIC LGK_TNT_FULL_PATH)
endif()
if (LGK_TNT_OUTPUT_LEVEL_DYNAMIC)
    target_compile_definitions(libgorak PUBLIC LGK_TNT_OUTPUT_LEVEL_DYNAMIC)
else()
    target_compile_definitions(libgorak PUBLIC LGK_TNT_OUTPUT_LEVEL_STATIC=${LGK_TNT_OUTPUT_LEVEL_STATIC})
endif()
if (LGK_TNT_TRAP_LABEL_PREFIX_OVERRIDE)
    target_compile_definitions(libgorak PUBLIC LGK_TNT_TRAP_LABEL_PREFIX_OVERRIDE=${LGK_TNT_TRAP_LABEL_PREFIX_OVERRIDE})
endif()
if (LGK_TNT_PRINT_DEFAULT_OVERRIDE)
    target_compile_definitions(libgorak PUBLIC LGK_TNT_PRINT_DEFAULT_OVERRIDE=${LGK_TNT_PRINT_DEFAULT_OVERRIDE})
endif()

# remove "lib" prefix since it's already part of the project name
set_target_properties(libgorak PROPERTIES PREFIX "")
